plugins {
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.72'
    id 'maven-publish'
}

group = 'net.cydhra'
version = '2.0.0'

repositories {
    jcenter()
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js') {
            compilations.main {
                configure([compileKotlinJs, compileTestKotlinJs]) {
                    kotlinOptions {
                        languageVersion = "1.3"
                        sourceMap = true
                        metaInfo = true
                        moduleKind = "umd"
                    }
                }
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation group: 'io.ktor', name: 'ktor-client-core', version: ktor_version
                implementation group: 'io.ktor', name: 'ktor-client-json', version: ktor_version
                implementation group: 'io.ktor', name: 'ktor-client-serialization', version: ktor_version
                implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-runtime-common', version: serialization_version
                implementation group: 'com.benasher44', name: 'uuid', version: '0.1.0'
                implementation group: 'com.soywiz.korlibs.klock', name: 'klock', version: klock_version
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation group: 'io.ktor', name: 'ktor-client-json-jvm', version: ktor_version
                implementation group: 'io.ktor', name: 'ktor-client-serialization-jvm', version: ktor_version
                implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-runtime', version: serialization_version
                implementation group: 'com.soywiz.korlibs.klock', name: 'klock-jvm', version: klock_version
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-runtime', version: serialization_version
                implementation group: 'io.ktor', name: 'ktor-client-cio', version: ktor_version
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation group: 'io.ktor', name: 'ktor-client-js', version: ktor_version
                implementation group: 'io.ktor', name: 'ktor-client-json-js', version: ktor_version
                implementation group: 'io.ktor', name: 'ktor-client-serialization-js', version: ktor_version
                implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-runtime-js', version: serialization_version
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}